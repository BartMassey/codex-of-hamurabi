
"The Codex of Hamurabi" by "Bart Massey"
[Copyright © 2012 Bart Massey]

The story headline is "A socioeconomic simulation".

Before printing the banner text, say "Govern ancient Sumer for 10 years...[paragraph break]"

After printing the banner text: say "Copyright © 2012 Bart Massey; based on David Ahl's famous 1978 BASIC game [italic type]HAMURABI,[roman type] which was in turn a port of Doug Dyment's 1968 FOCAL game [italic type]The Sumer Game.[roman type][paragraph break]".

The current year is a number that varies. The current year is 10.

Section - Metaphor

A metaphor is a kind of thing. It is fixed in place scenery. The description is usually "Even your Royal Highness should understand that [the item described] is just a metaphor."

The world is a region. Every room is in the world.

A metaphorical phrase is a kind of backdrop. The description is usually "[The item described] is a nice metaphor. However, your attempts to examine it only confuse you."

Section - The Throne Room

The Throne Room is a room. "[if the player is on the Throne of Hamurabi]You sit upon the [Throne of Hamurabi], as is your right as King.[paragraph break][end if]Sandstone splendor stretches upward around you in this vast and echoing hall. Soft [linen carpets], rich [wood furniture], and gold accessories and ornaments adorn the richly [tiled stone floor]. Warm natural [sunlight] filters in through various [high windows], contrasting pleasantly with the dim, cool surroundings." The sandstone splendor is a metaphorical phrase in the Throne Room. Understand "accessories" or "gold accessories" as the gold ornaments. Understand "surroundings" as the Throne Room.

Some throne room junk is a kind of thing. It is scenery. The description is usually "Very beautiful, and very appropriate to the space." Understand "space" as the Throne Room.

Instead of examining the Throne Room: try looking.

The linen carpets, wood furniture, gold ornaments, tiled stone floor and high windows are fixed in place throne room junk in the Throne Room.

Understand "carpet" as carpets. Understand "furnishings" or "table" or "chair" as furniture. Understand "accessory" or "accessories" or "ornament" as ornaments. Understand "floors" as floor. Understand "sun" or "light" or "sunbeam" or "sunbeams" as sunlight. Understand "window" as windows.

Some sunlight is a backdrop in the Throne Room. The description of sunlight is "The shafts of [sunlight] adorn the [Throne Room] with an appropriate grace".

Instead of taking sunlight: say "Wouldn't it be nice to be able to pocket some [sunlight] for the [dark nights] and [cold winters]? But it is not to be. The tighter you squeeze, the more it slips through your [fingers]." The dark nights are a metaphorical phrase in the world. The cold winters are a metaphorical phrase in the world. The fingers are a metaphorical phrase in the world.

The description of high windows is "The windows glow with the promise of your inevitable ascension."

Instead of taking windows: say "No glass. No frame. Just a hole. You take the window, but only in the abstract sense of possession; the window's emptiness is now yours."

Instead of taking throne room junk: say "These things have resided here for a thousand years. You leave them to reside a thousand more."

An enterable supporter called the Throne of Hamurabi is scenery in the Throne Room. The description is "This majestic gold and iron seat stands four cubits high." Does the player mean entering the Throne of Hamurabi: it is likely. Understand "gold seat" or "iron seat" or "gold and iron seat" as The Throne of Hamurabi. Four cubits are a backdrop in the world. The description is "You stand three and a half 'Sumerian cubits' high. A Kingly height indeed."

Section - Hamurabi the Player

King Hamurabi is a backdrop in the world. Instead of doing something with King Hamurabi: say "The 'Royal We' is majestic, but in your own mind you secretly refer to yourself as 'me'. Beneath you? Perhaps. But there it is." Does the player mean examining King Hamurabi: it is likely.

The description of the player is "You consider your stature, your grace and your excellence. [if the list of things worn by the player is empty]You consider your magnificent unclothed form[else]You consider your garments: [list of things worn by the player with definite articles][end if]. You, Hamurabi, are clearly a destined ruler." The player is on the Throne of Hamurabi. He is wearing a linen skirt, chestplate, golden sandals and an ornate crown. He carries the Rod of Hamurabi. 

The description of the skirt is "This is a simple linen garment, but the weave is incredibly fine and the needlework is impeccable. A stylish [gold clasp] closes the garment at the waist. Understated, but fit for a king."

The gold clasp is part of the skirt. The description is "This clever wrought-gold hook clips the edges of the skirt together reliably." Understand "wrought-gold" or "hook" or "clip" as the gold clasp.

The description of the chestplate is "This gold, bronze and iron chestplate is wrought with [depictions] of the glory of Hamurabi."

Some things called depictions are part of the chestplate. The description is "Gilt and finely-wrought reliefs of battle and of judgment, of honor and of power." Understand "reliefs" or "gilt reliefs" or "finely-wrought reliefs" as depictions.

The description of the golden sandals is "Gold and leather, with high [laces] and precious ornamentation." Understand "gold and leather sandals" as the golden sandals.

A thing called laces is part of the golden sandals.

The description of the golden crown is "Dotted with [precious stones], this golden crown rests easy on the head of the Great King." A head is a kind of thing. A head is part of every person. The description of the player's head is "Regal and aquiline." 

Some precious stones are part of the crown. The description is "Beautiful, and glowing with all the colors of the rainbow. Who knows what distant land these radiant gems once decorated?"

The description of the Rod is "This tall gold rod is capped with [a sparkling sapphire]. It is adorned with [mystic writing] and with [arcane symbols]."

A sparkling sapphire is part of the Rod. The description is "This sapphire is the size of [a plover's egg], and sparkles like [a star] in the night sky." A plover's egg is a metaphor. It is part of the Rod. A star is a metaphor. It is part of the Rod.

Some mystic writing is part of the Rod. The description is "The meaning is unclear, but the power of these runes is implicit. Let none brook the wrath of the great Hamurabi!" Understand "runes" as the mystic writing.

Some arcane symbols are part of the Rod. The description is "The symbols tell the story of the greatness of Hamurabi, for those with the wit to read it."

Section - Moving

Instead of going up: say "[one of]You cast your eyes up toward the Sumerian [sun]. For a moment, you imagine yourself as [an egret], flying free in [the wind]. Sighing, you return to your duties as King.[or]You wince at the obstinacy of your obligations, and continue to work.[stopping]". The sun is a backdrop. An egret is a metaphorical phrase in the world. The wind is a metaphorical phrase in the world.

Instead of going down: say "[one of]You dream of going down among the [common people]: of becoming one of them, merely [a cog] in [the vast machine] of Sumer.[paragraph break]Then you consider your position. Being a near-deity living in absurd luxury is, on reflection, a better course. You remain where you are.[or]There's nothing down there you want. You continue to work.[stopping]". Some common people are things. A cog is a metaphorical phrase in the world. The vast machine is a metaphorical phrase in the world.

Before going a direction when the player is on The Throne of Hamurabi: silently try exiting; say "(standing)[command clarification break]"

Instead of going a direction (called direction intended): say "[one of]You wander aimlessly around the [Throne Room]. So hard to concentrate. Oh well: that is the kind of thing, after all, that your [Steward] does admirably.[or]You start to head [direction intended], but you become distracted by the contemplation of your own greatness, and fall still.[or]You decide that you can't leave now; there is too much to be done.[or]You make [a mental note] to check out the [direction intended] part of the room later.[stopping]". A mental note is a metaphorical phrase in the world.

Section - The Steward

The Steward is in the Throne Room. "Your [Steward] awaits your commands with quiet dignity." The description is "This is the prime servant of The King; the man charged with carrying out the policies of Sumer. He is wearing [list of things worn by the Steward with indefinite articles]; he carries [list of things carried by the Steward with indefinite articles]." He is wearing a modest tunic and leather sandals. The description of the tunic is "Tied at the [waist] with a fine [braided leather belt], this garment is otherwise unadorned." The description of the leather sandals is "These sandals are well-made but well-worn, evidently the property of a Steward who has walked Sumer many times in the course of his duties." He is carrying the Staff of Stewardship, the Annual Journal, and an iron stylus. A waist is part of the Steward. Understand "servant" as the Steward.

The description of the Staff of Stewardship is "This elegant ebony staff reflects its [utilitarian roots] in a well-patched [scar]. The Steward is evidently not above striking that which needs striking." Understand "ebony staff" as the Staff of Stewardship. The scar is part of the staff. "Just a scratch, but fraught with meaning." Understand "scratch" as the scar. Some utilitarian roots are a metaphorical phrase in the world.

The description of the stylus is "This three-sided iron tool has an angular tip. It is well-suited to embossing and inscription." Understand "tool" as the stylus. The angular tip is part of the stylus. The description is "This wedge-shaped tip makes an angular mark."

The braided leather belt is part of the tunic. The description is "Intricately wrought, and in good condition. A respectable garment for a respectable man."


The description of the Journal is "In this place, in this time, books have an almost palpable power. Take this one, for instance. The [italic type][Annual Journal][roman type] chronicles the history of the Kingdom of Sumer: its population and food supply, its immigration and death, its land and its inhabitants all the way down to the rats in the granaries." Understand "book" or "books" as the Journal.

A thing called pages is part of the Journal. The description is "The [italic type]Annual Journal's[roman type] pages are sheets of purest gold, embossed by [stylus] with a record of the history of Sumer over a year's time." Understand "sheets" or "gold sheets" as pages. Understand "history" or "record" as the Journal.

Section - Units of Record

Population is a kind of value. 1 subject (singular) specifies population. 10 subjects (plural) specifies population.

Acreage is a kind of  value. 1 acre (singular) specifies acreage. 10 acres (plural) specifies acreage.

Grain is a kind of value. 1 bushel (singular) specifies grain. 10 bushels (plural) specifies grain.

Grain/acreage is a kind of value. 1 bushel/acre (singular) specifies grain/acreage. 10 bushels/acre (plural) specifies grain/acreage. Acreage times grain/acreage specifies grain.

Section - The Record

Table of State of Sumer
year	population	acreage		stored
10	95 subjects	1000 acres	2800 bushels
with 9 blank rows

Table of Records of Sumer
year		starved		plague		immigrants	yield		harvested
number	population	population	population	grain/acreage	grain
with 10 blank rows

Table of Edicts of Hamurabi
year		buy land	feed people	plant crops
number	acreage	population	acreage
with 10 blank rows

Section - Times and Seasons

The current year is a number that varies. The current year is usually 10.

A season name is a kind of value. The season names are Winter, Spring, Summer and Fall.

The current season is a season name that varies. The current season is usually Fall.

Awaiting next season is a truth state that varies. Awaiting next season is usually false.

A Governing Season is a recurring scene. A Governing Season begins when play begins. A Governing Season begins when a Governing Season ends. A Governing Season ends when awaiting next season is true. When a Governing Season begins when awaiting next season is true: say "[italic type][the season description corresponding to a season index of the current season in the Table of Season Descriptions][roman type]"; now awaiting next season is false.

Table of Season Descriptions
season index	season description
Fall			"Ah, Fall. The time of [harvest] in the land. The [farmers] and [slaves] of Hamurabi bring in the [bounty] that feeds the [Kingdom]."
Winter		"Winter is a time of rest in the [Kingdom]. All await the rebirth of Spring, brought forth by the Divine Hamurabi."
Spring		"Oh, the joy of Spring! A time of planting and growing! King Hamurabi sows [his seed] upon the land, and will soon reap [the harvest]."
Summer		"The Summers are harsh and hot in Sumer; the God-King Hamurabi rules justly and mercilessly."

Some farmers and some slaves and the kingdom and his seed and the harvest and bounty are metaphorical phrases in the world.

When a Governing Season ends:
	if the current season is the last value of season name:
		increment the current year;
		now the current season is the first value of season name;
	otherwise:
		now the current season is the season name after the current season.

Section - Engine

Part 1 - Set up

Chapter 1 - Input Status

[this is used to determine - in the story - if the 3 inputs are successful or not.]

inputStatus is a kind of value.
inputStatus are PurchaseSuccess, 
	PurchaseFail_NotEnoughLand, PurchaseFail_NotEnoughGrain,
	FeedSuccess,
	FeedFail_MustFeedPeople, FeedFail_NotEnoughGrain,
	PlantSuccess,
	PlantFail_MustPlantSomething, PlantFail_NotEnoughGrain, PlantFail_NotEnoughPeople,
	NoStatus.

Chapter 2 - Parameter

Section 1 - Definition

ParameterThing is a kind of thing.
[how many years to play]
ParameterThing has a number called _yearsToPlay.
[starting values]
ParameterThing has a number called _year. [starting year]
ParameterThing has a number called _population. [people]
ParameterThing has a number called _stored. [number of bushels]
ParameterThing has a number called _acreage. [acres]
[input values]
ParameterThing has a number called _purchase. [acres]
ParameterThing has a number called _feed. [number of bushels]
ParameterThing has a number called _plant. [number of bushels]
[computed values]
ParameterThing has a number called _immigrants. [babies and immigrants]
ParameterThing has a number called _plagued. [population that was infected]
ParameterThing has a number called _landPrice. [number of bushels/acre]
ParameterThing has a number called _landCost. [number of bushels = landPrice * _purchase]
ParameterThing has a number called _yield. [multiplier for harvest]
ParameterThing has a number called _harvest. [number of bushels]
ParameterThing has a number called _ratEaten. [number of bushels]
ParameterThing has a number called _starved. [people]
[running totals]
ParameterThing has a number called _runningPopulation. [people]
ParameterThing has a number called _runningStored. [number of bushels]
ParameterThing has a number called _runningAcreage. [acres]
[input status]
ParameterThing has a inputStatus called _currentInputStatus.
[failure number]
ParameterThing has a number called _minimumPopulation.

Section 2 - Global Instance

currentParameter is a ParameterThing.

Section 3 - Initialize Global Instance

To _initializeParameter (_p - a ParameterThing):
	now _yearsToPlay of _p is 10;
	[start of play increments year]
	now _year of _p is 0;
	[need to play a little trick here, put initial values in running]
	now _runningPopulation of _p is EP in row 1 of Table of Sumer History;
	now _runningStored of _p is EG in row 1 of Table of Sumer History;
	now _runningAcreage of _p is EA in row 1 of Table of Sumer History;
	now _currentInputStatus of _p is NoStatus.

Part 2 - The Engine Pieces

Chapter 1 - Functions

Section 1 - Immigration

To decide what number is _calcImmigrants (_acreage - a number) and (_stored - a number) and (_population - a number):
	let _r be a random number between 1 and 5;
	let _a20 be 20 * _acreage;
	let _a20s be _a20 + _stored;
	let _x be _r * _a20s;
	let _p100 be 100 * _population;
	let _p100p1 be _p100 + 1;
	let _immigrants be _x / _p100p1;
	decide on _immigrants;

To decide what number is _populationAfterImmigration (_population - a number) moved (_immigrants - a number):
	decide on _population + _immigrants;

Section 2 - Plague

To decide what number is _calcPlague (_population - a number):
	if a random chance of 15 in 100 succeeds:
		let _killed be _population / 2;
		decide on _killed;
	otherwise:
		decide on 0;

To decide what number is _populationAfterPlague (_population - a number) died (_killed - a number):
	decide on _population - _killed;

Section 3 - Purchase

To decide what number is _landPricePerAcre:
	let _rc be a random number between 1 and 10;
	let _lprice be _rc + 17;
	decide on _lprice;
	
[check if selling acreage (_purchaseAcreage is a negative number) that there is enough _originalAcreage]
To decide what number is _acreageAfterPurchase (_originalAcreage - a number) buying (_purchasedAcreage - a number):
	decide on _originalAcreage + _purchasedAcreage;
	
To decide what number is _costOfPurchase (_purchase - a number) paying (_landPricePerAcre - a number):
	decide on _purchase * _landPricePerAcre;
	
[check that _cost <= _stored]
To decide what number is _storedAfterPurchase (_stored - a number) paying (_cost - a number):
	decide on _stored - _cost;

Section 4 - Feed

[check that feed >= 0 and feed <= stored]
To decide what number is _storedAfterFeedPeople (_feed - a number) from (_stored - a number):
	let _storedAfterFeed be _stored - _feed;
	decide on _storedAfterFeed;

Section 5 - Planting / Harvest
	
[check plant >= 0 and plant <= 2 * stored and plant <= 10 * population]
To decide what number is _storedAfterPlanting (_plant - a number) from (_stored - a number):
	let _storedAfterPlant be _stored - _plant;
	decide on _storedAfterPlant;

To decide what number is _calcYield:
	decide on a random number between 1 and 5;
		
To decide what number is _calcHarvest (_plant - a number) given (_yield - a number):
	let _yield be a random number between 1 and 5;
	let _harvest be _plant * _yield;
	decide on _harvest;
	
To decide what number is _storedAfterHarvest (_stored - a number) harvested (_harvest - a number):
	decide on _stored + _harvest;

Section 6 - Rats

To decide what number is _ateByRats (_stored - a number):
	let _rc be a random number between 1 and 5;
	if _rc is an odd number:
		let _eaten be _stored / _rc;
		decide on _eaten;
	otherwise:
		decide on 0;

To decide what number is _storedAfterFeedRats (_stored - a number) when eat (_eaten - a number):
	let _storedAfterRats be _stored - _eaten;
	decide on _storedAfterRats;

Section 7 - Starvation
		
To decide what number is _starvedPopulation (_population - a number) when fed (_feed - a number):
	let _starved be _population - _feed;
	decide on _starved;
	
To decide what number is _populationAfterStarved (_population - a number) died (_starved - a number):
	decide on _population - _starved;

Section 8 - Minimum Population

To decide what number is _minimumPopulationThisTurn (_population - a number):
	let _x be _population * 45;
	let _x be _x / 100;
	decide on _population - _x;
	
Chapter 2 - Input Actions

Section 1 - InputStatus

inputStatus is a kind of value.
inputStatus are 
	PurchaseSuccess, 
	PurchaseFail_NotEnoughLand, PurchaseFail_NotEnoughGrain, 
	PurchaseFail_TrySelling, PurchaseFail_TryBuying,
	FeedSuccess,
	FeedFail_MustFeedPeople, FeedFail_NotEnoughGrain, FeedFail_OverFeedPeople,
	PlantSuccess,
	PlantFail_MustPlantSomething, PlantFail_NotEnoughGrain, PlantFail_NotEnoughPeople.

Section 2 - Purchase / Sell

[purchase/sell acreage]
Understand "buy [number] acres/--" as _buyingInput.
Understand "purchase [number] acres/--" as _buyingInput.
_buyingInput is an action applying to one number.
Check _buyingInput:
	unless _gameState of currentParameter is PurchaseState:
		say "[_canNotBuyNow]";
		stop the action;
	[think you wanted to sell]
	if number understood < 0:
		say "[_purchaseFail_trySelling]";
		now _currentInputStatus of currentParameter is PurchaseFail_TrySelling;
		stop the action;
	[if number understood > _runningAcreage of currentParameter:
		now _currentInputStatus of currentParameter is PurchaseFail_NotEnoughLand;
		stop the action;]
	let _tp be _costOfPurchase number understood paying _landPrice of currentParameter;
	[must have enough grain to buy the land]
	if _tp > _runningStored of currentParameter:
		say "[_purchaseFail_notEnoughGrain]";
		now _currentInputStatus of currentParameter is PurchaseFail_NotEnoughGrain;
		stop the action;
		
Carry out _buyingInput:
	now _purchase of currentParameter is number understood;
	now _currentInputStatus of currentParameter is PurchaseSuccess;

Understand "sell [number] acres/--" as _sellingInput.
_sellingInput is an action applying to one number.
Check _sellingInput:
	unless _gameState of currentParameter is PurchaseState:
		say "[_canNotSellNow]";
		stop the action;
	[think you wanted to buy]
	if number understood < 0:
		say "[_purchaseFail_tryBuying]";
		now _currentInputStatus of currentParameter is PurchaseFail_TryBuying;
		stop the action;
	[cant sell more than have]
	if number understood > _runningAcreage of currentParameter:
		say "[_purchaseFail_notEnoughLand]";
		now _currentInputStatus of currentParameter is PurchaseFail_NotEnoughLand;
		stop the action;
	let _tp be _costOfPurchase number understood paying _landPrice of currentParameter;
	[if _tp > _runningStored of currentParameter:
		now _currentInputStatus of currentParameter is PurchaseFail_NotEnoughGrain;
		stop the action;]

Carry out _sellingInput:
	let _x be number understood;
	let _x be _x * -1;
	now _purchase of currentParameter is _x;
	now _currentInputStatus of currentParameter is PurchaseSuccess;

Section 3 - Feed

[feed population]
Understand "feed [number] subjects/people/--" as _feedingInput.
_feedingInput is an action applying to one number.
Check _feedingInput:
	unless _gameState of currentParameter is FeedState:
		say "[_canNotFeedNow]";
		stop the action;
	[trying to feed more people than have]
	if number understood > _runningPopulation of currentParameter:
		say "[_feedFail_overFeedPeople]";
		now _currentInputStatus of currentParameter is FeedFail_OverFeedPeople;	
		stop the action;
	[if would be friendly to force feeding the minimumPopulation]
	if number understood <= 0:
		say "[_feedFail_mustFeedPeople]";
		now _currentInputStatus of currentParameter is FeedFail_MustFeedPeople;	
		stop the action;
	[takes 1 bushel to feed 1 person, so must have enough to feed the number specified]
	if number understood >= _runningStored of currentParameter:
		say "[_feedFail_notEnoughGrain].";
		now _currentInputStatus of currentParameter is FeedFail_NotEnoughGrain;
		stop the action;
		
Carry out _feedingInput:
	now _feed of currentParameter is number understood;
	now _currentInputStatus of currentParameter is FeedSuccess;

Section 4 - Plant

[plant crops input is number of acres]	
Understand "plant [number] arces/--" as _plantingInput.
_plantingInput is an action applying to one number.
Check _plantingInput:
	unless _gameState of currentParameter is PlantState:
		say "[_canNotPlantNow]";
		stop the action;
	[can't plant negative land]
	if number understood < 0:
		say "[_plantFail_mustPlantSomething]";
		now _currentInputStatus of currentParameter is PlantFail_MustPlantSomething;
		stop the action;
	[can't plant more than 2 acres per bushel of grain]
	if number understood / 2 > _runningStored of currentParameter:
		say "[_plantFail_notEnoughGrain]";
		now _currentInputStatus of currentParameter is PlantFail_NotEnoughGrain;
		stop the action;
	[one person can plant 10 acres, so got enough people?]
	if number understood / 10 > _runningPopulation of currentParameter:
		say "[_plantFail_notEnoughPeople]";
		now _currentInputStatus of currentParameter is PlantFail_NotEnoughPeople;
		stop the action;
		
Carry out _plantingInput:
	now _plant of currentParameter is number understood;
	now _currentInputStatus of currentParameter is PlantSuccess;

Chapter 3 - Parts of Engine

Section 1 - Setup Parameter

To _runAYear_Part0 (_p - a ParameterThing):
	now _yearsToPlay of _p is _yearsToPlay of _p - 1;
	now _year of _p is _year of _p + 1;
	now _population of _p is _runningPopulation of _p;
	now _stored of _p is _runningStored of _p;
	now _acreage of _p is _runningAcreage of _p;
	now _purchase of _p is 0;
	now _feed of _p is 0;
	now _plant of _p is 0;
	now _immigrants of _p is 0;
	now _plagued of _p is 0;
	now _landPrice of _p is 0;
	now _landCost of _p is 0;
	now _yield of _p is 0;
	now _harvest of _p is 0;
	now _ratEaten of _p is 0;
	now _starved of _p is 0;
	[don't need to update running....]
	now _currentInputStatus of _p is NoStatus;
	now _minimumPopulation of _p is _minimumPopulationThisTurn _population of _p;
	[_reportFullStatus;]
	
Section 2 - Immigration and Plague

To _runAYear_Part1 (_p - a ParameterThing):
	now _runningPopulation of _p is _population of _p;
	now _runningStored of _p is _stored of _p;
	now _runningAcreage of _p is _acreage of _p;
	[population change by immigration]	
	now _immigrants of _p is _calcImmigrants 
										_runningAcreage of _p and 
										_runningStored of _p and 
										_runningPopulation of _p;
	now _runningPopulation of _p is _populationAfterImmigration _runningPopulation of _p moved _immigrants of _p;
	[population change by plague]	
	now _plagued of _p is _calcPlague _runningPopulation of _p;
	now _runningPopulation of _p is _populationAfterPlague _runningPopulation of _p died _plagued of _p;
	[_reportFullStatus;]
	
Section 3 - Purchase Land

To _runAYear_Part2_A (_p - a ParameterThing):
	[stored change by buy land]
	now _landPrice of _p is _landPricePerAcre;
	[_reportFullStatus;]
	
[input _purchase : _purchase of _p]
	
To _runAYear_Part2_B (_p - a ParameterThing):
	now _runningAcreage of _p is _acreageAfterPurchase _runningAcreage of _p buying _purchase of _p;
	now _landCost of _p is _costOfPurchase _purchase of _p paying _landPrice of _p;
	now _runningStored of _p is _runningStored of _p - _landCost of _p;
	[_reportFullStatus;]

Section 4 - Feed Population

To _runAYear_Part3 (_p - a ParameterThing):
	[stored change by feeding]
	[input _feed : _feed of _p]
	now _runningStored of _p is _storedAfterFeedPeople _feed of _p from _runningStored of _p;
	[_reportFullStatus;]
	
Section 5 - Plant, Harvast, Rats and Starvation

To _runAYear_Part4 (_p - a ParameterThing):
	[stored change by planting]
	[input _plant : _plant of _p]
	now _runningStored of _p is _storedAfterPlanting _plant of _p from _runningStored of _p;
	[stored change by harvest]
	now _yield of _p is _calcYield;
	now _harvest of _p is _calcHarvest _plant of _p given _yield of _p;
	[TODO: shouldn't harvest be added to running stored????]
	now _runningStored of _p is _storedAfterHarvest _runningStored of _p harvested _harvest of _p;
	[stored change by rats]
	now _ratEaten of _p is _ateByRats _runningStored of _p;
	now _runningStored of _p is _storedAfterFeedRats _runningStored of _p when eat _ratEaten of _p;
	[population change by starvation]
	now _starved of _p is _starvedPopulation _runningPopulation of _p when fed _feed of _p;
	[TODO: shouldn't population be updated with starved????]
	now _runningPopulation of _p is _populationAfterStarved _runningPopulation of _p died _starved of _p;
	[_reportFullStatus;]
	
Chapter 4 - History of Data

Section 1 - The Table

[Y = year, SP = starting population, SG = starting stored grain, SA = starting acreage, PH = purchase, FD = feed, PT = plant, 
	IM = immigrants, PG = plagued, LP = land price, LC = land cost, YD = yield, HV = harvest, RE = rats eaten, 
	SV = starved people, EP = ending population, EG = ending stored grain, EA = ending acreage]
[Year 0 is used to set up the initial conditions]
Table of Sumer History
Y 	SP	SG	SA	PH	FD	PT	IM	PG	LP	LC	YD	HV	RE	SV	EP	EG	EA
0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	2000	1000	1500
with 10 blank rows

Section 2 - Table Manipulation

To _fillHistoryRow (_p - ParameterThing):
	[make sure there is a blank row]
	if number of blank rows in Table of Sumer History <= 0:
		say "TABLE ERROR: no blank rows to record history.";
		stop the action;
	choose a blank row in Table of Sumer History;
	now Y entry is _year of _p;
	now SP entry is _population of _p;
	now SG entry is _stored of _p;
	now SA entry is _acreage of _p;
	now PH entry is _purchase of _p;
	now FD entry is _feed of _p;
	now PT entry is _plant of _p;
	now IM entry is _immigrants of _p;
	now PG entry is _plagued of _p;
	now LP entry is _landPrice of _p;
	now LC entry is _landCost of _p;
	now YD entry is _yield of _p;
	now HV entry is _harvest of _p;
	now RE entry is _ratEaten of _p;
	now SV entry is _starved of _p;
	now EP entry is _runningPopulation of _p;
	now EG entry is _runningStored of _p;
	now EA entry is _runningAcreage of _p;
	sort Table of Sumer History in Y column order;

To _clearHistory:
	repeat with N running from 2 to the number of rows in Table of Sumer History:
		blank out the whole row;

To _setInitialPopulation (_population - a number):
	now EP in row 1 of Table of Sumer History is _population;
	
To _setInitialStoredGrain (_grain - a number):
	now EG in row 1 of Table of Sumer History is _grain;
	
To _setInitialAcreage (_acreage - a number):
	now EA in row 1 of Table of Sumer History is _acreage;
	
Section 3 - Table Actions

Understand "report short history" as _reportingShortHistory.
Understand "short history" as _reportingShortHistory.	
Understand "report short" as _reportingShortHistory.
Understand "r short" as _reportingShortHistory.
_reportingShortHistory is an action out of world.
Check _reportingShortHistory:
	if number of filled rows in Table of Sumer History is less than 2:
		say "[_noHistoryAvailable]" instead.
			
Carry out _reportingShortHistory:
	repeat with N running from 2 to the number of filled rows in Table of Sumer History:
		say "[_aYearOfHistory N]";
	
Understand "report edict/edicts" as _reportingEdicts.
Understand "r edict/edicts" as _reportingEdicts.
_reportingEdicts is an action out of world.
Check _reportingEdicts:
	if number of filled rows in Table of Sumer History is less than 2:
		say "[_noEdictsAvailable]" instead.
		
Carry out _reportingEdicts:
	repeat with N running from 2 to the number of filled rows in Table of Sumer History:
		say "[_aYearOfEdicts N]";
	
Part 3 - State based Engine

Chapter 1 - States

Section 1 - Game State Value

Game State is a kind of value.
Game State is StartOfGameState, StartOfYearState, PurchaseState, FeedState, PlantState, EndOfYearState, EndOfGameState.

Section 2 - ParameterThing carries the Game State

ParameterThing has a Game State called _gameState.

Section 3 - When play begins - setup initial state

When play begins:
	now _gameState of currentParameter is StartOfGameState.

Chapter 2 - State Machine

Section 1 - Execute State Change

To _executeStateChange (_nextState - Game State):
	if _gameState of currentParameter is:
		-- StartOfGameState:
			if _nextState is:
				-- StartOfYearState:
					say "[_leaveStartOfGameState]";
					[do this BEFORE change state]
					_initializeParameter currentParameter;
					now _gameState of currentParameter is StartOfYearState;
					_runAYear_Part0 currentParameter;
					say "[_enterStartOfYearState]";
				-- otherwise:
					say "STATE MACHINE ERROR: [_gameState of currentParameter] to [_nextState].";
					stop the action;
		-- StartOfYearState:
			if _nextState is:
				-- PurchaseState:
					say "[_leaveStartOfYearState]";
					say "[_enterPurchaseState]";
					now _gameState of currentParameter is PurchaseState;
					_runAYear_Part1 currentParameter;
					_runAYear_Part2_A currentParameter;
				-- otherwise:
					say "STATE MACHINE ERROR: [_gameState of currentParameter] to [_nextState].";
					stop the action;
		-- PurchaseState:
			if _nextState is:
				-- FeedState:
					say "[_leavePurchaseState]";
					say "[_enterFeedState]";
					now _currentInputStatus of currentParameter is NoStatus;
					now _gameState of currentParameter is FeedState;
					_runAYear_Part2_B currentParameter;
				-- otherwise:
					say "STATE MACHINE ERROR: [_gameState of currentParameter] to [_nextState].";
					stop the action;
		-- FeedState:
			if _nextState is:
				-- PlantState:
					say "[_leaveFeedState]";
					say "[_enterPlantState]";
					now _currentInputStatus of currentParameter is NoStatus;
					now _gameState of currentParameter is PlantState;
					_runAYear_Part3 currentParameter;
				-- otherwise:
					say "STATE MACHINE ERROR: [_gameState of currentParameter] to [_nextState].";
					stop the action;
		-- PlantState:
			if _nextState is:
				-- EndOfYearState:
					say "[_leavePlantState]";
					say "[_enterEndOfYearState]";
					now _currentInputStatus of currentParameter is NoStatus;
					now _gameState of currentParameter is EndOfYearState;
					_runAYear_Part4 currentParameter;
					_fillHistoryRow currentParameter;
				-- otherwise:
					say "STATE MACHINE ERROR: [_gameState of currentParameter] to [_nextState].";
					stop the action;
		-- EndOfYearState:
			if _nextState is:
				-- StartOfYearState:	
					say "[_leaveEndOfYearState]";
					now _gameState of currentParameter is StartOfYearState;
					_runAYear_Part0 currentParameter;
					say "[_enterStartOfYearState]";
				-- EndOfGameState:
					say "[_endOfGameStateAnnouncement]";
					now _gameState of currentParameter is EndOfGameState;
				-- otherwise:
					say "STATE MACHINE ERROR: [_gameState of currentParameter] to [_nextState].";
					stop the action;
		-- EndOfGameState:
			say "STATE MACHINE ERROR: [_gameState of currentParameter] to [_nextState].";
			stop the action;

Section 2 - Check State

[used in testing to keep track of state]	
To _updatePrompt:
	do nothing.
				
To _checkState:															
	if _gameState of currentParameter is:
		-- PurchaseState:
			if _currentInputStatus of currentParameter is PurchaseSuccess:
				_executeStateChange FeedState;
		-- FeedState:
			if _currentInputStatus of currentParameter is FeedSuccess:
				_executeStateChange PlantState;
		-- PlantState:
			if _currentInputStatus of currentParameter is PlantSuccess:
				_executeStateChange EndOfYearState;
		-- EndOfYearState:
			continue the action;
		-- EndOfGameState:
			continue the action;
	_updatePrompt;

Section 3 - Every turn check the state
	
Every turn:
	_checkState;

Chapter 3 - Proceed Action

[used to move from 
	StartOfGameState to StartOfYearState
	StartOfYearState to PurchaseState and 
	EndOfYearState to StartOfYearState/LoserState/WinnerState]

Understand "proceed" as _continuingGame.
_continuingGame is an action applying to nothing.
Check _continuingGame:
	if _gameState of currentParameter is PurchaseState: 
		say "[_purchaseToProceed]";
		stop the action;
	if _gameState of currentParameter is FeedState: 
		say "[_feedToProceed]";
		stop the action;
	if _gameState of currentParameter is PlantState: 
		say "[_plantToProceed]";
		stop the action;
		
Carry out _continuingGame:
	if _gameState of currentParameter is:
		-- StartOfGameState:
			_executeStateChange StartOfYearState;
		-- StartOfYearState:
			_executeStateChange PurchaseState;
		-- EndOfYearState:
			if _runningPopulation of currentParameter < _minimumPopulation of currentParameter:
				_executeStateChange EndOfGameState;
			otherwise:
				if _yearsToPlay of currentParameter < 0:
					_executeStateChange EndOfGameState;
				otherwise:
					_executeStateChange StartOfYearState;

Part 4 - To Be Used in Game

Chapter 1 - Data access for Game (do not override in game)

Section 1 - Initial Setup of Game

To Set Initial Population (population - a number):
	_setInitialPopulation population.
	
To Set Initial Stored Grain (grain - a number):
	_setInitialStoredGrain grain.
	
To Set Initial Acreage (acreage - a number):
	_setInitialAcreage acreage.
	
Section 2 - Basic Information

To say Current Year: 
	say "[_year of currentParameter]".
To say Current Population:
	say "[_runningPopulation of currentParameter] subjects".
To say Current Stored Grain:
	say "[_runningStored of currentParameter] bushels".
To say Current Acreage:
	say "[_runningAcreage of currentParameter] acres".
To say Minimum Population:
	say "[_minimumPopulation of currentParameter] subjects".

Section 3 - Purchase State Limits

To say Maximum Land Purchasable:
	unless _gameState of currentParameter is PurchaseState:
		say "LOGIC ERROR: don't ask for Maximum Land Purchasable unless in purchase state.";
		stop the action;
	say "[_runningStored of currentParameter / _landPrice of currentParameter] acres";

[bushels per acre]
To say Cost Of Land:
	unless _gameState of currentParameter is PurchaseState:
		say "LOGIC ERROR: don't ask for Cost Of Land unless in purchase state.";
		stop the action;
	say "[_landPrice of currentParameter] bushels per acre";

Section 4 - Plant State Limits

To say Maximum Land Plantable:
	unless _gameState of currentParameter is PlantState:
		say "LOGIC ERROR: don't ask for Maximum Land Plantable unless in plant state.";
		stop the action;
	[1 bushel per 2 acres]
	let _x be _runningStored of currentParameter * 2;
	[10 acres per person]
	let _y be _runningPopulation of currentParameter * 10;
	let _maxpossible be 	_x;
	if _x > _y:
		now _maxpossible is _y;
	[can't plant more land than have]
	if _maxpossible > _runningAcreage of currentParameter:
		now _maxpossible is _runningAcreage of currentParameter;
	say "[_maxpossible] acres";
	
Chapter 2 - Error Statements (override in game)

Section 1 - Invalid Command for Current State

[Define these within the game, basically says the command the player is
	trying to use is not allowed when in this state. So, can't plant when 
	in purchasing state.]

To say _canNotBuyNow:
	say "AUTHOR ERROR: Override 'To say _canNotBuyNow'.".
To say _canNotSellNow:
	say "AUTHOR ERROR: Override 'To say _canNotSellNow'.".
To say _canNotFeedNow:		
	say "AUTHOR ERROR: Override 'To say _canNotFeedNow'.".
To say _canNotPlantNow:		
	say "AUTHOR ERROR: Override 'To say _canNotPlantNow'.".
To say _purchaseToProceed:
	say "AUTHOR ERROR: Override 'To say _purchaseToProceed'.".
To say _feedToProceed:
	say "AUTHOR ERROR: Override 'To say _feedToProceed'.".
To say _plantToProceed:
	say "AUTHOR ERROR: Override 'To say _plantToProceed'.".
	
Section 2 - Invalid Value for Command

To say _purchaseFail_notEnoughLand:
	say "AUTHOR ERROR: Override 'To say _purchaseFail_notEnoughLand'.".
To say _purchaseFail_notEnoughGrain:
	say "AUTHOR ERROR: Override 'To say _purchaseFail_notEnoughGrain'.".
To say _purchaseFail_trySelling:
	say "AUTHOR ERROR: Override 'To say _purchaseFail_trySelling'.".
To say _purchaseFail_tryBuying:
	say "AUTHOR ERROR: Override 'To say _purchaseFail_tryBuying'.".
To say _feedFail_mustFeedPeople:
	say "AUTHOR ERROR: Override 'To say _feedFail_mustFeedPeople'.".
To say _feedFail_notEnoughGrain:
	say "AUTHOR ERROR: Override 'To say _feedFail_notEnoughGrain'.".
To say _feedFail_overFeedPeople:
	say "AUTHOR ERROR: Override 'To say _feedFail_overFeedPeople'.".
To say _plantFail_mustPlantSomething:
	say "AUTHOR ERROR: Override 'To say _plantFail_mustPlantSomething' (negative plant).".
To say _plantFail_notEnoughGrain:
	say "AUTHOR ERROR: Override 'To say _plantFail_notEnoughGrain'.".
To say _plantFail_notEnoughPeople:
	say "AUTHOR ERROR: Override 'To say _plantFail_notEnoughPeople'.".

Section 3 - Short History Errors

To say _noHistoryAvailable:
	say "AUTHOR ERROR: Override 'To say _noHistoryAvailable'.";

To say _noEdictsAvailable:
	say "AUTHOR ERROR: Override 'To say _noEdictsAvailable'.";
	
Chapter 3 - Say Statements (override in game)

Section 1 - History
	
To say _aYearOfHistory (_index - a number):
	choose row _index in Table of Sumer History;
	say "AUTHOR ERROR: Override 'To say _aYearOfHistory (_index - a number)'.";
	[say "In the year [Y entry], the starting population was [SP entry] subjects and the ending population was [EP entry] subjects. The starting stored grain was [SG entry] bushels and the ending stored grain was [EG entry] bushels. The starting acreage was [SA entry] acres and the ending acreage was [EA entry] acres.[paragraph break]";]

To say _aYearOfEdicts (_index - a number):
	choose row _index in Table of Sumer History;
	say "AUTHOR ERROR: Override 'To say _aYearOfEdicts (_index - a number)'.";
	[say "In the year [Y entry], the you ordered [if PH entry < 0]the sale of [-1 * PH entry] acres[otherwise if PH entry is 0]no sale or purchase of acreage[otherwise]the purchase of [PH entry] acres[end if]. You ordered [FD entry] subjects fed and planted [PT entry] acres.[paragraph break]";	]

Section 2 - Enter / Leave State Announcements

To say _leaveStartOfGameState:
	say "AUTHOR ERROR: Override 'To say _leaveStartOfGameState'.";
To say _enterStartOfYearState:
	say "AUTHOR ERROR: Override 'To say _enterStartOfYear'.";
To say _leaveStartOfYearState:
	say "AUTHOR ERROR: Override 'To say _leaveStartOfYearState'.";
To say _enterPurchaseState:
	say "AUTHOR ERROR: Override 'To say _enterPurchaseState'.";
To say _leavePurchaseState:
	say "AUTHOR ERROR: Override 'To say _leavePurchaseState'.";
To say _enterFeedState:
	say "AUTHOR ERROR: Override 'To say _enterFeedState'.";
To say _leaveFeedState:
	say "AUTHOR ERROR: Override 'To say _leaveFeedState'.";
To say _enterPlantState:
	say "AUTHOR ERROR: Override 'To say _enterPlantState'.";
To say _leavePlantState:
	say "AUTHOR ERROR: Override 'To say _leavePlantState'.";
To say _enterEndOfYearState:
	say "AUTHOR ERROR: Override 'To say _enterEndOfYearState'.";
To say _leaveEndOfYearState:
	say "AUTHOR ERROR: Override 'To say _leaveEndOfYearState'.";
	
Section 3 - End Game Announcement

To say _endOfGameStateAnnouncement:
	say "AUTHOR ERROR: Override 'To say _endOfGameStateAnnouncement'.";
	
Chapter 4 - When Play Begins (put this in the game) - not for release

When play begins:
	Set Initial Population 200;
	Set Initial Stored Grain 300;
	Set Initial Acreage 400;

Part 5 - Testing - not for release

Chapter 1 - Helper Reports

To _updatePrompt:
	now the command prompt is "[_gameState of currentParameter] > ";
	
Understand "status" as _reportingFullStatus.
_reportingFullStatus is an action out of world.
Carry out _reportingFullStatus:
	say "[line break]";
	_reportFullStatus;
	say "[line break]".

To _reportStatus:
	say "The current year is [_year of currentParameter].";
	say "The current population is [_runningPopulation of currentParameter].";
	say "The current food available is [_runningStored of currentParameter] bushels.";
	say "The current land is [_runningAcreage of currentParameter] acres.";

To _reportFullStatus:
	say "Current Status.";
	say "Year is [_year of currentParameter].";
	say "Population is [_population of currentParameter].";
	say "Stored is [_stored of currentParameter].";
	say "Acreage is [_acreage of currentParameter].";
	say "Purchase is [_purchase of currentParameter].";
	say "Feed is [_feed of currentParameter].";
	say "Plant is [_plant of currentParameter].";
	say "Immigrants is [_immigrants of currentParameter].";
	say "Land Price is [_landPrice of currentParameter].";
	say "Land Cost is [_landCost of currentParameter].";
	say "Yield is [_yield of currentParameter].";
	say "Harvest is [_harvest of currentParameter].";
	say "Rats is [_ratEaten of currentParameter].";
	say "Starved is [_starved of currentParameter].";
	say "R Population is [_runningPopulation of currentParameter].";
	say "R Stored is [_runningStored of currentParameter].";
	say "R Acreage is [_runningAcreage of currentParameter].";
	say "Min Population is [_minimumPopulation of currentParameter].";

Chapter 2 - Test Game

Section 1 - When play begins

When play begins:
	Set Initial Population 2345;
	Set Initial Stored Grain 3456;
	Set Initial Acreage 456;

Section 2 - Override messages

To say _canNotBuyNow:
	say "Override 'To say _canNotBuyNow'.".
To say _canNotSellNow:
	say "Override 'To say _canNotSellNow'.".
To say _canNotFeedNow:		
	say "Override 'To say _canNotFeedNow'.".
To say _canNotPlantNow:		
	say "Override 'To say _canNotPlantNow'.".
To say _purchaseToProceed:
	say "Override 'To say _purchaseToProceed'.".
To say _feedToProceed:
	say "Override 'To say _feedToProceed'.".
To say _plantToProceed:
	say "Override 'To say _plantToProceed'.".

To say _purchaseFail_notEnoughLand:
	say "Override 'To say _purchaseFail_notEnoughLand'.".
To say _purchaseFail_notEnoughGrain:
	say "Override 'To say _purchaseFail_notEnoughGrain'.".
To say _purchaseFail_trySelling:
	say "Override 'To say _purchaseFail_trySelling'.".
To say _purchaseFail_tryBuying:
	say "Override 'To say _purchaseFail_tryBuying'.".
To say _feedFail_mustFeedPeople:
	say "Override 'To say _feedFail_mustFeedPeople'.".
To say _feedFail_notEnoughGrain:
	say "Override 'To say _feedFail_notEnoughGrain'.".
To say _feedFail_overFeedPeople:
	say "Override 'To say _feedFail_overFeedPeople'.".
To say _plantFail_mustPlantSomething:
	say "Override 'To say _plantFail_mustPlantSomething' (negative plant).".
To say _plantFail_notEnoughGrain:
	say "Override 'To say _plantFail_notEnoughGrain'.".
To say _plantFail_notEnoughPeople:
	say "Override 'To say _plantFail_notEnoughPeople'.".

To say _aYearOfHistory (_index - a number):
	choose row _index in Table of Sumer History;
	say "Override 'To say _aYearOfHistory (_index - a number)'.";
	say "In the year [Y entry], the starting population was [SP entry] subjects and the ending population was [EP entry] subjects. The starting stored grain was [SG entry] bushels and the ending stored grain was [EG entry] bushels. The starting acreage was [SA entry] acres and the ending acreage was [EA entry] acres.[paragraph break]";

To say _aYearOfEdicts (_index - a number):
	choose row _index in Table of Sumer History;
	say "Override 'To say _aYearOfEdicts (_index - a number)'.";
	say "In the year [Y entry], the you ordered [if PH entry < 0]the sale of [-1 * PH entry] acres[otherwise if PH entry is 0]no sale or purchase of acreage[otherwise]the purchase of [PH entry] acres[end if]. You ordered [FD entry] subjects fed and planted [PT entry] acres.[paragraph break]";
	
To say _leaveStartOfGameState:
	say "Override 'To say _leaveStartOfGameState'.";
To say _enterStartOfYearState:
	say "Override 'To say _enterStartOfYear'.";
To say _leaveStartOfYearState:
	say "Override 'To say _leaveStartOfYearState'.";
To say _enterPurchaseState:
	say "Override 'To say _enterPurchaseState'.";
To say _leavePurchaseState:
	say "Override 'To say _leavePurchaseState'.";
To say _enterFeedState:
	say "Override 'To say _enterFeedState'.";
To say _leaveFeedState:
	say "Override 'To say _leaveFeedState'.";
To say _enterPlantState:
	say "Override 'To say _enterPlantState'.";
To say _leavePlantState:
	say "Override 'To say _leavePlantState'.";
To say _enterEndOfYearState:
	say "Override 'To say _enterEndOfYearState'.";
To say _leaveEndOfYearState:
	say "Override 'To say _leaveEndOfYearState'.";

To say _endOfGameStateAnnouncement:
	say "Override 'To say _endOfGameStateAnnouncement'.";

